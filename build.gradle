import java.util.stream.Collectors

plugins {
    id('java')
    id('maven-publish')
    id('com.github.johnrengelman.shadow') version('5.2.0')
    id('com.github.breadmoirai.github-release') version('2.3.1')
}

group("$projectGroup")
version("$projectVersion")

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.mongodb:mongodb-driver-sync:$mongoDriverVersion")
    testImplementation("org.mongodb:mongodb-driver-sync:$mongoDriverVersion")

    // Lombok
    compileOnly("org.projectlombok:lombok:$lombokVersion")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")

    testCompileOnly("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")

    // Jupiter test dependencies
    testImplementation("org.junit.jupiter:junit-jupiter-engine:$jupiterVersion")
    testImplementation("org.slf4j:slf4j-simple:$slf4jVersion")
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile).configureEach {
    options.fork = true
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    repositories {
        maven {
            url('https://reposilite.koboo.eu/releases')
            credentials {
                username(System.getenv('REPO_USER'))
                password(System.getenv('REPO_TOKEN'))
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

githubRelease {
    token(System.getenv('GITHUB_TOKEN').toString())
    draft = true

    releaseAssets jar.destinationDir.listFiles()

    body { """\
## Links to $version

* [Check the Documentation](https://koboo.gitbook.com/en2do)
* [Check the JavaDocs](https://reposilite.koboo.eu/javadoc/releases/eu/koboo/en2do/$version/)
* [Add the Maven dependency](https://reposilite.koboo.eu/#/releases/eu/koboo/en2do/$version/)
* [Download artifacts from Jenkins](https://jenkins.koboo.eu/job/en2do/job/Build%20and%20Publish%20(main)/)

${
        changelog().call()
            .readLines()
            .stream()
            .limit(10)
            .map { "- $it" }
            .collect(Collectors.joining('\n', '## Changelog\n', ''))
    }
- And more..
""" }
}

project.tasks.shadowJar.finalizedBy(project.tasks.javadocJar)
project.tasks.shadowJar.finalizedBy(project.tasks.sourcesJar)
project.tasks.publish.dependsOn(project.tasks.shadowJar)
project.tasks.githubRelease.dependsOn(project.tasks.shadowJar)
